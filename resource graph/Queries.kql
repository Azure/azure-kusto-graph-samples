//Get all nodes with type "User" of the "Contoso" tenant.
let tenants=dynamic(["Contoso"]);
NodesLKV(interestingTenants=tenants)
| where NodeType == "User"

//Get all nodes with type "User" of all tenants, project the "Age" property only.
let properties = dynamic(["Age"]);
NodesLKV(interestingProperties=properties)
| where NodeType == "User"

//Get all nodes of all tenants on a specific point in time.
Nodes(datetime(2024-10-20))

//Get the last known state of all edges with the Label "Reader" of a resource graph.
EdgesLKV()
| where Labels has "Reader"
| project EdgeType, Labels, Properties

//Get all resources of the "Contoso" tenant with the "Age" property on nodes and the "LocationFilter" property on edges. Visualize using Kusto Explorer (https://aka.ms/ke).
let tenants=dynamic(["Contoso"]);
let nodeProperties = dynamic(["Age"]);
let edgeProperties = dynamic(["LocationFilter"]);
GraphLKV(interestingTenants=tenants, interestingEdgeProperties=edgeProperties, interestingNodeProperties=nodeProperties)

//Get the number of resources grouped by resource type connected to a management group.
let tenants=dynamic(["Contoso"]);
GraphLKV(interestingTenants=tenants)
| graph-match (mg)-[e*1..10]->(resource)
    where mg.NodeType == "ManagementGroup"
    project nodeType = resource.NodeType, FQDN = resource.FQDN
| summarize dcount(FQDN) by nodeType

//Statistics on resource assignments for "Alice" in "Contoso"
let interestingUser = "Alice";
let tenants=dynamic(["Contoso"]);
let G = GraphLKV();
let groupBasedAccess = view() {
    G
    | graph-match (resource)<-[authorized_on*1..4]-(group)-[hasMember*1..255]->(user)
        where user.NodeId == interestingUser and user.NodeType == "User" and hasMember.EdgeType == "has_member" and group.NodeType == "Group" and
            authorized_on.EdgeType in ("authorized_on", "contains_resource")
        project Username=user.NodeId, userFQDN=user.FQDN, resourceTenantId=resource.TenantId, resourceType=resource.NodeType, resourceName=resource.NodeId
};
let directAccess = view() {
    G
    | graph-match (resource)<-[authorized_on*1..4]-(user)
        where user.NodeId == interestingUser and user.NodeType == "User" and
            authorized_on.EdgeType in ("authorized_on", "contains_resource")
        project Username=user.NodeId, userFQDN=user.FQDN, resourceTenantId=resource.TenantId, resourceType=resource.NodeType, resourceName=resource.NodeId
};
union groupBasedAccess, directAccess
| summarize Username = take_any(Username), ResourceTypes=dcount(resourceType), Resources=dcount(resourceName) by userFQDN

//Check Bob, who has direct and group based access to resources
let interestingUser = "Bob";
let G = GraphLKV();
let groupBasedAccess = view() {
    G
    | graph-match (resource)<-[authorized_on*1..4]-(group)-[hasMember*1..255]->(user)
        where user.NodeId == interestingUser and user.NodeType == "User" and hasMember.EdgeType == "has_member" and group.NodeType == "Group" and
            authorized_on.EdgeType in ("authorized_on", "contains_resource")
        project Username=user.NodeId, userFQDN=user.FQDN, resourceTenantId=resource.TenantId, resourceType=resource.NodeType, resourceName=resource.NodeId
};
let directAccess = view() {
    G
    | graph-match (resource)<-[authorized_on*1..4]-(user)
        where user.NodeId == interestingUser and user.NodeType == "User" and
            authorized_on.EdgeType in ("authorized_on", "contains_resource")
        project Username=user.NodeId, userFQDN=user.FQDN, resourceTenantId=resource.TenantId, resourceType=resource.NodeType, resourceName=resource.NodeId
};
union withsource=Assignment groupBasedAccess, directAccess
| summarize Username = take_any(Username), Tenants=dcount(resourceTenantId), ResourceTypes=dcount(resourceType), Resources=dcount(resourceName) by userFQDN, Assignment

//Check multiple users and create a result on direct or group based access to resources
let interestingUsers = dynamic(["Bob", "Alice"]);
let G = GraphLKV();
let groupBasedAccess = view() {
    G
    | graph-match (resource)<-[authorized_on*1..4]-(group)-[hasMember*1..255]->(user)
        where user.NodeId in (interestingUsers) and user.NodeType == "User" and hasMember.EdgeType == "has_member" and group.NodeType == "Group" and
            authorized_on.EdgeType in ("authorized_on", "contains_resource")
        project Username=user.NodeId, userFQDN=user.FQDN, resourceTenantId=resource.TenantId, resourceType=resource.NodeType, resourceName=resource.NodeId
};
let directAccess = view() {
    G
    | graph-match (resource)<-[authorized_on*1..4]-(user)
        where user.NodeId in (interestingUsers) and user.NodeType == "User" and
            authorized_on.EdgeType in ("authorized_on", "contains_resource")
        project Username=user.NodeId, userFQDN=user.FQDN, resourceTenantId=resource.TenantId, resourceType=resource.NodeType, resourceName=resource.NodeId
};
union withsource=Assignment groupBasedAccess, directAccess
| summarize Username = take_any(Username), Tenants=dcount(resourceTenantId), ResourceTypes=dcount(resourceType), Resources=dcount(resourceName) by Assignment, userFQDN

//Provide all permissions of a user on VMs
let interestingUsers = dynamic(["Alice", "Bob"]);
let interestingResourceTypes = dynamic(["VirtualMachine"]);
let tenants=dynamic(["Contoso"]);
let G = GraphLKV();
let groupBasedAccess = view() {
    G
    | graph-match (resource)<-[authorized_on*0..4]-(group)-[hasMember*1..255]->(user)
        where user.NodeId in (interestingUsers) and user.NodeType == "User" and hasMember.EdgeType == "has_member" and group.NodeType == "Group" and
            authorized_on.EdgeType in ("authorized_on", "contains_resource") and resource.NodeType in (interestingResourceTypes)
        project Username=user.NodeId, userFQDN=user.FQDN, resourceTenantId=resource.TenantId, resourceType=resource.NodeType, resourceName=resource.NodeId, AuthorizationLabels=authorized_on.Labels
};
let directAccess = view() {
    G
    | graph-match (resource)<-[authorized_on*1..4]-(user)
        where user.NodeId in (interestingUsers) and user.NodeType == "User" and
        authorized_on.EdgeType in ("authorized_on", "contains_resource") and resource.NodeType in (interestingResourceTypes)
    project Username=user.NodeId, userFQDN=user.FQDN, resourceTenantId=resource.TenantId, resourceType=resource.NodeType, resourceName=resource.NodeId, AuthorizationLabels=authorized_on.Labels
};
union withsource=Assignment groupBasedAccess, directAccess
| mv-apply AuthLabel=AuthorizationLabels on (
    where array_length(AuthLabel) > 0
)
| mv-expand AuthLabel to typeof(string)
| summarize Permissions=make_set(AuthLabel) by userFQDN, resourceTenantId, resourceType, resourceName

GraphLKV()

//Which user need to be notified if a specific VM is broken?
let interestingResourceTypes = dynamic(["VirtualMachine"]);
let interestingResources = dynamic(["VM1"]);
let tenants=dynamic(["Contoso"]);
GraphLKV(interestingTenants=tenants)
| graph-match cycles=none (resource)-[e*1..255]-(user)
    where resource.NodeId in (interestingResources) and resource.NodeType in (interestingResourceTypes) and user.NodeType == "User"
    project Username=user.NodeId
| distinct Username